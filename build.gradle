apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'rs.pedjaapps'
version = '1.0'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.2'
    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            configuration = configurations.deployerJars
            //repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            repository(url: "scp://maven.android-forever.com/var/www/vhosts/pedjaapps.net/maven.android-forever.com") {
                authentication(userName: androidforeverSshUsername, privateKey: androidforeverSshPrivateKey, passphrase: androidforeverSshPassphrase)
            }

            pom.project {
                artifactId 'json-stream-parser'
                name 'json-stream-parser'
                packaging 'jar'
                description 'Wrapper around Jakson Json Parser that makes parsing json from InputStream easier'
                url 'https://github.com/pedja1/JsonStreamParser'

                scm {
                    url 'scm:git@github.com:pedja1/JsonStreamParser.git'
                    connection 'scm:git@github.com:pedja1/JsonStreamParser.git'
                    developerConnection 'scm:git@github.com:pedja1/JsonStreamParser.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'pedja1'
                        name 'Predrag ÄŒokulov'
                    }
                }
            }
        }
    }
}